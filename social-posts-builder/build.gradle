group 'com.puppycrawl.tools'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'checkstyle'

sourceCompatibility = 1.8

task allJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.puppycrawl.tools.PostsBuilder'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

checkstyle {
    project.ext.checkstyleVersion = '7.0'

    config resources.text.fromString(new URL ("https://raw.githubusercontent.com/checkstyle/checkstyle/checkstyle-${checkstyleVersion}/config/checkstyle_checks.xml").getText())

    configProperties = [
        'checkstyle.suppressions.file' : 'config/suppressions.xml',
        'checkstyle.header.file' : 'https://raw.githubusercontent.com/checkstyle/checkstyle/master/config/java.header',
        'checkstyle.regexp.header.file' : 'https://raw.githubusercontent.com/checkstyle/checkstyle/master/config/java_regexp.header',
        'checkstyle.importcontrol.file' : 'config/import-control.xml',
    ]
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.jdom', name: 'jdom', version: '2.0.0'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'org.twitter4j', name: 'twitter4j-core', version: '4.0.4'
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"
}
